// Dolittle
// カメネコ本6(p28,29)
module ! "Vec2" "Actor" "UI" "Timer" "Util" "Turtle" "Figure" "DOM" "Color" "Japanese" [
    // タートルをつくる
    ボール＝タートル！作る  ペンなし。
    ボール！４５  向き  ０  −１００  位置。
    // 部品をつくる
    かめた＝タートル！作る。
    かめた！（青）線の色  ２０  線の太さ。
    ブロック＝かめた！８０  歩く  図形を作る  −４０  ０  位置。
    床＝かめた！（赤）線の色  ３８０  歩く  図形を作る  −１９０  −１９０  位置。
    壁＝かめた！９０  向き（緑）線の色  ４００  歩く  ９０  右回り  ４００  歩く  ９０  右回り  ４００  歩く  図形を作る  −２００  −２００  位置。
    ブロック！−１８０  １６０  位置。
    ブロック２＝ブロック！作る（黄色）塗る  ９０  ０  移動する。
    ブロック３＝ブロック２  ！作る（紫）塗る  ９０  ０  移動する。
    ブロック４＝ブロック３  ！作る（水色）塗る  ９０  ０  移動する。
    パドル＝かめた！０  向き  １００  歩く（青）図形を作る  −５０  −１６０  位置。
    // タイマーでボールを動かす
    時計＝タイマー！作る  ０．０２  間隔  ８  時間。
    時計！「ボール！１０  歩く。」実行。
    // 衝突の定義
    得点＝フィールド！0  作る。
    ボール：衝突＝タートル：跳ね返る。
    ブロック：衝突＝「自分！消える。得点！１０  増やす。」。
    床：衝突＝「|相手|
        相手！消える。
        時計！中断。
    」。
    // パドルを操作する
    左ボタン＝ボタン！"  左  "  "LEFT"  作る。
    左ボタン：動作＝「パドル！−４０  ０  移動する。」。
    右ボタン＝ボタン！"  右  "  "RIGHT"  作る。
    右ボタン：動作＝「パドル！４０  ０  移動する。」。
    // 勝敗の判定
    時計！「
        「（得点！読む）＞＝４０」！なら「
            ラベル！"  ゲームクリア  "  作る。
        」そうでなければ「
            ラベル！"  ゲームオーバー  "  作る。
        」実行。
    」最後に実行。
] require。
