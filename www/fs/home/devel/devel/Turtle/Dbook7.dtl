module!"Vec2" "Actor" "UI" "Turtle" "Figure" "DOM" "Util"  "Timer" "Color" "Japanese"
[
    
    //// Dolittle
    //// ドリトル本「ch7. シューティングゲーム」（p39−45）
    //// （30−33行目）オブジェクトの複製の際、かめたの変身が解かれる
    //// （36−38行目）テキストの記述方法では正しく動作しない
    //// （51行目）フィールド（書く）
    
    //タイマー：次に実行＝「｜b｜！（b）実行」。
    //タイマー：最後に実行＝「｜b｜！１  回数（b）実行  ０  回数」。    
    
    カメ太＝タートル！作る 90 左回り ペンなし 0 -150 位置。
    左=ボタン！"左" "left" 作る -200 -180 位置。
    左:動作＝「カメ太！-20 0 移動する」。
    右=ボタン！"右" "right" 作る 50 -180 位置。
    右:動作＝「カメ太！20 0 移動する」。
    
    カメ太：発射＝「タイマー！作る「自分！20 歩く」実行」。
    発射=ボタン！"発射" "UP" 作る 100 45 大きさ  -50 -180 位置。
    発射:動作＝「カメ太！作る 70 歩く "rocket.gif" 変身する 発射」。
    
    敵たち＝配列！作る。
    敵＝タートル！作る "ayumiAka.gif" 変身する ペンなし 90 右回り。
    
    //// テストプログラム（属性の確認）
    //敵：タイプ＝90。
    //敵たち！（敵！作る 0 10 位置）書く。
    //敵２＝敵！作る 100 歩く。
    //敵２！（敵２：タイプ）右回り。
    
    敵たち！（敵！-300 200 位置）書く。
    敵たち！（敵！作る -200 200 位置）書く。
    敵たち！（敵！作る -100 200 位置）書く。
    敵たち！（敵！作る 0 200 位置）書く。
    敵たち！（敵！作る 100 200 位置）書く。
    
    時計＝タイマー！作る 1 間隔 。
    時計!6 回数「敵たち！「|敵|敵！30 0 移動する」each」実行。
    時計!1 回数「敵たち！「|敵|敵！0 -30 移動する」each」実行。
    時計!6 回数「敵たち！「|敵|敵！-30 0 移動する」each」実行。
    //// 正しく動く記述方法
    //時計!「敵たち！「|敵|敵！10 0 移動する」それぞれ実行」6  実行。
    //時計!「敵たち！「|敵|敵！0 -10 移動する」それぞれ実行」1  実行。
    //時計!「敵たち！「|敵|敵！-10 0 移動する」それぞれ実行」6  実行。
    
    カメ太：衝突＝「自分！消える」。
    敵：衝突＝「自分！消える。敵たち！（自分）消す.:window:console!(敵たち)log.」。
    
    制限時間＝15。
    カウントダウン=ラベル！作る 150 0 移動する。
    終了時計＝タイマー！作る 1 間隔（制限時間）時間。
    終了時計！「|n|カウントダウン！（制限時間-n）書く。 ////フィールド
        「（敵たち！要素数?） ＝＝ 0」！なら「
            ラベル！"おめでとう！！！"作る -100 200 位置。 ////ラベル
            終了時計！中断
        」実行
    」実行。
]require.
