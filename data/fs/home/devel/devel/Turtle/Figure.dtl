//NOGENERATOR
// system ! "Actor" use.
Figure=Actor !create.
Figure:initialize=[|parent|
    [parent] ! then [
        element=parent:lineG.
        element ! remove.
        pos=parent:lineOrig.
        dir=0.
        !setLocalShapeAsLines.
    ] else [
        element=! "g" createSVGElem.// (svg_g) appendTo.
        !(element) (this:__proto__:element) copySVGElem.
        polygonElem=undef.
        !findPolygonElem.        
    ] execute.
    isShowing=false.
    !appear.
].
Figure:findPolygonElem=[|;p|
    p=element ! "polygon" find.
    [p:length>0] ! then [polygonElem=p] execute.
].
Figure:setLocalShapeAsPolygon=[|;t j lx ly ls|
    t=this.
    ls=element ! "line" find.
    localShape=Polygon! create.
    ls ! [
        j=(t:$)! (this) create.
        lx=j ! "x1" attr.lx=lx-0.
        ly=j ! "y1" attr.ly=ly-0.
        (t:localShape) ! (lx) (ly) addVertex.
    ] each.
    lx=j ! "x2" attr.lx=lx-0.
    ly=j ! "y2" attr.ly=ly-0.
    localShape ! (lx) (ly) addVertex.
].
Figure:setLocalShapeAsLines=[|;t j x1 y1 x2 y2 p1 p2 w|
    t=this.
    localShape=ShapeGroup ! create.
    element ! "line" find [
        j=(t:$)! (this) create.
        x1=j ! "x1" attr.x1=x1-0.
        y1=j ! "y1" attr.y1=y1-0.
        p1=(t:Vec2) ! (x1) (y1) create.
        x2=j ! "x2" attr.x2=x2-0.
        y2=j ! "y2" attr.y2=y2-0.
        p2=(t:Vec2) ! (x2) (y2) create.
        w=j ! "data-width" attr.w=w-0.
        (t:localShape) ! ((t:Polygon) !(p1) (p2) (w) fromLine) add.
    ] each.
    localShape.
].

Figure:getLocalShape=[localShape].

Figure:getCrashShape=[|;tr|
    !getLocalShape (dir) (pos) transform.
].

Figure:paint=[|col;buf ls|
    [polygonElem] ! then [] else [
        buf="".
        ls=element ! "line" find.
        [(ls:length)>=2] ! then [
            ! setLocalShapeAsPolygon.
            localShape ! [|v|
                buf=buf+(v:x)+","+(v:y)+" ".
            ] each.
            polygonElem=! "polygon" createSVGElem 
            "points" (buf) attr (element) appendTo.
            ls ! remove.
        ] else [
            ls ! "style" (
            "stroke:"+col+";"+
            "stroke-width:"+(ls ! "data-width" attr)
            ) attr.
        ] execute.
    ] execute.
    [polygonElem] ! then [polygonElem ! "fill" (col) attr] execute.
    this.
].
Figure:makeGroup=[
    Group ! (arguments) create.
].

